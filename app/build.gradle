apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlinx-serialization'
apply plugin: "androidx.navigation.safeargs.kotlin"
apply plugin: 'com.google.gms.oss.licenses.plugin'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId "jp.maskedronin.bitwatcher"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode 10000
        versionName "1.0.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "room.schemaLocation"  : "$projectDir/schemas".toString(),
                        "room.incremental"     : "true",
                        "room.expandProjection": "true"
                ]
            }
        }
        buildConfigField("String", "DB_NAME", "\"$database_name\"")
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"

            def db_passphrase = "passphrase"
            buildConfigField("String", "DB_PASSPHRASE", "\"$db_passphrase\"")
            resValue("string", "DB_PASSWORD_$database_name", "\"$db_passphrase\"")
        }
        release {
            buildConfigField("String", "DB_PASSPHRASE", "\"\"")
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    dataBinding {
        enabled true
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:0.20.0"

    // androidx
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.0.0"
    implementation 'androidx.fragment:fragment-ktx:1.3.0-alpha03'
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.2.0"
    implementation "androidx.security:security-crypto:1.0.0-beta01"
    def room_version = "2.2.5"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    def nav_version = "2.2.1"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    def dagger_version = "2.24"
    implementation "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    implementation "com.google.dagger:dagger-android:$dagger_version"
    kapt "com.google.dagger:dagger-android-processor:$dagger_version"
    implementation "com.google.dagger:dagger-android-support:$dagger_version"

    implementation 'com.squareup.retrofit2:retrofit:2.7.2'
    implementation 'com.jakewharton.retrofit:retrofit2-kotlinx-serialization-converter:0.5.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.4.0'

    implementation 'net.zetetic:android-database-sqlcipher:4.3.0'

    implementation "com.github.hadilq.liveevent:liveevent:1.2.0"

    implementation 'com.google.android.material:material:1.1.0'

    implementation 'com.jakewharton.threetenabp:threetenabp:1.2.2'
    implementation "com.jakewharton.timber:timber:4.7.1"

    implementation 'com.google.android.gms:play-services-oss-licenses:17.0.0'

    def hyperion_version = "0.9.27"
    debugImplementation "com.willowtreeapps.hyperion:hyperion-core:$hyperion_version"
    debugImplementation "com.willowtreeapps.hyperion:hyperion-attr:$hyperion_version"
    debugImplementation "com.willowtreeapps.hyperion:hyperion-build-config:$hyperion_version"
    debugImplementation "com.willowtreeapps.hyperion:hyperion-crash:$hyperion_version"
    debugImplementation "com.willowtreeapps.hyperion:hyperion-disk:$hyperion_version"
    debugImplementation "com.willowtreeapps.hyperion:hyperion-geiger-counter:$hyperion_version"
    debugImplementation "com.willowtreeapps.hyperion:hyperion-measurement:$hyperion_version"
    debugImplementation "com.willowtreeapps.hyperion:hyperion-phoenix:$hyperion_version"
    debugImplementation "com.willowtreeapps.hyperion:hyperion-recorder:$hyperion_version"
    debugImplementation "com.willowtreeapps.hyperion:hyperion-shared-preferences:$hyperion_version"

    debugImplementation 'com.amitshekhar.android:debug-db-encrypt:1.0.6'

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.2'

    testImplementation 'junit:junit:4.12'
    testImplementation 'androidx.test:core:1.2.0'
    // Kotlinx.serialization が Unit Testだと使えないため
    testImplementation 'com.google.code.gson:gson:2.8.6'

    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
